defmodule Transport.UserNotifier do
  use Phoenix.Swoosh, view: TransportWeb.EmailView

  @moduledoc """
  Module in charge of building emails.
  First all admin emails, then all user emails.
  """

  def contact(email, subject, question) do
    new()
    |> from({"PAN, Formulaire Contact", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :contact_email))
    |> reply_to(email)
    |> subject(subject)
    |> text_body(question)
  end

  def feedback(rating, explanation, email, feature) do
    rating_t = %{like: "jâ€™aime", neutral: "neutre", dislike: "mÃ©content"}

    reply_email = if email, do: email, else: Application.fetch_env!(:transport, :contact_email)

    feedback_content = """
    Vous avez un nouvel avis sur le PAN.
    FonctionnalitÃ© : #{feature}
    Notation : #{rating_t[rating]}
    Adresse e-mail : #{email}

    Explication : #{explanation}
    """

    new()
    |> from({"Formulaire feedback", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :contact_email))
    |> reply_to(reply_email)
    |> subject("Nouvel avis pour #{feature} : #{rating_t[rating]}")
    |> text_body(feedback_content)
  end

  def bnlc_consolidation_report(subject, body, file_url) do
    report_content = """
    #{body}
    <br/><br/>
    ğŸ”— <a href="#{file_url}">Fichier consolidÃ©</a>
    """

    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.get_env(:transport, :bizdev_email))
    |> subject(subject)
    |> html_body(report_content)
  end

  def datasets_without_gtfs_rt_related_resources(datasets) do
    links =
      Enum.map_join(datasets, "\n", fn %DB.Dataset{slug: slug, custom_title: custom_title} ->
        link = TransportWeb.Router.Helpers.dataset_url(TransportWeb.Endpoint, :details, slug)
        "* #{custom_title} - #{link}"
      end)

    text_body = """
    Bonjour,

    Les jeux de donnÃ©es suivants contiennent plusieurs GTFS et des liens entre les ressources GTFS-RT et GTFS sont manquants :

    #{links}

    Lâ€™Ã©quipe transport.data.gouv.fr

    """

    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :contact_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Jeux de donnÃ©es GTFS-RT sans ressources liÃ©es")
    |> text_body(text_body)
  end

  def datasets_climate_resilience_bill_inappropriate_licence(datasets) do
    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :bizdev_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Jeux de donnÃ©es article 122 avec licence inappropriÃ©e")
    |> render_body("datasets_climate_resilience_bill_inappropriate_licence.html", %{datasets: datasets})
  end

  def new_datagouv_datasets(datasets, duration) do
    text_body = """
    Bonjour,

    Les jeux de donnÃ©es suivants ont Ã©tÃ© ajoutÃ©s sur data.gouv.fr dans les derniÃ¨res #{duration}h et sont susceptibles d'avoir leur place sur le PAN :

    #{Enum.map_join(datasets, "\n", &link_and_name/1)}

    ---
    Vous pouvez consulter et modifier les rÃ¨gles de cette tÃ¢che : https://github.com/etalab/transport-site/blob/master/apps/transport/lib/jobs/new_datagouv_datasets_job.ex
    """

    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :bizdev_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Nouveaux jeux de donnÃ©es Ã  rÃ©fÃ©rencer - data.gouv.fr")
    |> text_body(text_body)
  end

  def expiration_admin(records) do
    text_body = """
    Bonjour,

    Voici un rÃ©sumÃ© des jeux de donnÃ©es arrivant Ã  expiration

    #{Enum.map_join(records, "\n---------------------\n", &expiration_str/1)}
    """

    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :bizdev_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Jeux de donnÃ©es arrivant Ã  expiration")
    |> text_body(text_body)
  end

  def inactive_datasets(reactivated_datasets, inactive_datasets, archived_datasets) do
    reactivated_datasets_str = fmt_reactivated_datasets(reactivated_datasets)
    inactive_datasets_str = fmt_inactive_datasets(inactive_datasets)
    archived_datasets_str = fmt_archived_datasets(archived_datasets)

    text_body =
      """
      Bonjour,
      #{inactive_datasets_str}
      #{reactivated_datasets_str}
      #{archived_datasets_str}

      Il faut peut Ãªtre creuser pour savoir si c'est normal.

      """

    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :bizdev_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Jeux de donnÃ©es supprimÃ©s ou archivÃ©s")
    |> text_body(text_body)
  end

  def oban_failure(worker) do
    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :tech_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
    |> subject("Ã‰chec de job Oban : #{worker}")
    |> text_body("Un job Oban #{worker} vient d'Ã©chouer, il serait bien d'investiguer.")
  end

  defp expiration_str({delay, records}) do
    datasets = Enum.map(records, fn {%DB.Dataset{} = d, _} -> d end)

    dataset_str = fn %DB.Dataset{} = dataset ->
      "#{link_and_name_2(dataset)} (#{expiration_notification_enabled_str(dataset)}) #{climate_resilience_str(dataset)}"
      |> String.trim()
    end

    """
    Jeux de donnÃ©es #{delay_str(delay, :pÃ©rimant)} :

    #{Enum.map_join(datasets, "\n", &dataset_str.(&1))}
    """
  end

  def expiration_notification_enabled_str(%DB.Dataset{} = dataset) do
    if has_expiration_notifications?(dataset) do
      "âœ… notification automatique"
    else
      "âŒ pas de notification automatique"
    end
  end

  defp climate_resilience_str(%DB.Dataset{} = dataset) do
    if DB.Dataset.climate_resilience_bill?(dataset) do
      "âš–ï¸ğŸ—ºï¸ article 122"
    else
      ""
    end
  end

  def has_expiration_notifications?(%DB.Dataset{} = dataset) do
    DB.NotificationSubscription.reason(:expiration)
    |> DB.NotificationSubscription.subscriptions_for_reason_dataset_and_role(dataset, :producer)
    |> Enum.count() > 0
  end

  defp fmt_inactive_datasets([]), do: ""

  defp fmt_inactive_datasets(inactive_datasets) do
    datasets_str = Enum.map_join(inactive_datasets, "\n", &link_and_name_2(&1))

    """
    Certains jeux de donnÃ©es ont disparus de data.gouv.fr :
    #{datasets_str}
    """
  end

  defp fmt_reactivated_datasets([]), do: ""

  defp fmt_reactivated_datasets(reactivated_datasets) do
    datasets_str = Enum.map_join(reactivated_datasets, "\n", &link_and_name_2(&1))

    """
    Certains jeux de donnÃ©es disparus sont rÃ©apparus sur data.gouv.fr :
    #{datasets_str}
    """
  end

  defp fmt_archived_datasets([]), do: ""

  defp fmt_archived_datasets(archived_datasets) do
    datasets_str = Enum.map_join(archived_datasets, "\n", &link_and_name_2(&1))

    """
    Certains jeux de donnÃ©es sont indiquÃ©s comme archivÃ©s sur data.gouv.fr :
    #{datasets_str}

    #{count_archived_datasets()} jeux de donnÃ©es sont archivÃ©s. Retrouvez-les dans le backoffice : #{backoffice_archived_datasets_url()}
    """
  end

  @spec link_and_name_2(DB.Dataset.t()) :: binary()
  def link_and_name_2(%DB.Dataset{custom_title: custom_title} = dataset) do
    link = link(dataset)

    " * #{custom_title} - #{link}"
  end

  def count_archived_datasets do
    DB.Dataset.archived() |> DB.Repo.aggregate(:count, :id)
  end

  defp backoffice_archived_datasets_url do
    TransportWeb.Router.Helpers.backoffice_page_url(TransportWeb.Endpoint, :index, %{"filter" => "archived"}) <>
      "#list_datasets"
  end

  @doc """
  iex> email_subject(7)
  "Jeu de donnÃ©es arrivant Ã  expiration"
  iex> email_subject(0)
  "Jeu de donnÃ©es arrivant Ã  expiration"
  iex> email_subject(-3)
  "Jeu de donnÃ©es pÃ©rimÃ©"
  """
  def email_subject(delay) when delay >= 0 do
    "Jeu de donnÃ©es arrivant Ã  expiration"
  end

  def email_subject(delay) when delay < 0 do
    "Jeu de donnÃ©es pÃ©rimÃ©"
  end

  @doc """
  iex> delay_str(0, :pÃ©rimant)
  "pÃ©rimant demain"
  iex> delay_str(0, :pÃ©riment)
  "pÃ©riment demain"
  iex> delay_str(2, :pÃ©rimant)
  "pÃ©rimant dans 2 jours"
  iex> delay_str(2, :pÃ©riment)
  "pÃ©riment dans 2 jours"
  iex> delay_str(-1, :pÃ©rimant)
  "pÃ©rimÃ© depuis hier"
  iex> delay_str(-1, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis hier"
  iex> delay_str(-2, :pÃ©rimant)
  "pÃ©rimÃ©s depuis 2 jours"
  iex> delay_str(-2, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis 2 jours"
  iex> delay_str(-60, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis 60 jours"
  """
  @spec delay_str(integer(), :pÃ©riment | :pÃ©rimant) :: binary()
  def delay_str(0, verb), do: "#{verb} demain"
  def delay_str(1, verb), do: "#{verb} dans 1 jour"
  def delay_str(d, verb) when d >= 2, do: "#{verb} dans #{d} jours"
  def delay_str(-1, :pÃ©rimant), do: "pÃ©rimÃ© depuis hier"
  def delay_str(-1, :pÃ©riment), do: "sont pÃ©rimÃ©es depuis hier"
  def delay_str(d, :pÃ©rimant) when d <= -2, do: "pÃ©rimÃ©s depuis #{-d} jours"
  def delay_str(d, :pÃ©riment) when d <= -2, do: "sont pÃ©rimÃ©es depuis #{-d} jours"

  @doc """
  iex> resource_titles([%DB.Resource{title: "B"}])
  "B"
  iex> resource_titles([%DB.Resource{title: "B"}, %DB.Resource{title: "A"}])
  "A, B"
  """
  def resource_titles(resources) do
    resources
    |> Enum.sort_by(fn %DB.Resource{title: title} -> title end)
    |> Enum.map_join(", ", fn %DB.Resource{title: title} -> title end)
  end

  # Starting from here, all the functions are used to send emails to users

  def resources_changed(email, subject, %DB.Dataset{} = dataset) do
    email
    |> common_email_options()
    |> subject(subject)
    |> render_body("resources_changed.html", %{dataset: dataset})
  end

  def new_comments_reuser(%DB.Contact{email: email}, datasets) do
    email
    |> common_email_options()
    |> subject("Nouveaux commentaires sur transport.data.gouv.fr")
    |> render_body("new_comments_reuser.html", %{datasets: datasets})
  end

  def new_comments_producer(email, comments_number, comments) do
    email
    |> common_email_options()
    |> subject("#{comments_number} nouveaux commentaires sur transport.data.gouv.fr")
    |> render_body("new_comments_producer.html", comments_with_context: comments)
  end

  def promote_reuser_space(email) do
    email
    |> common_email_options()
    |> subject("Gestion de vos favoris dans votre espace rÃ©utilisateur")
    |> render_body("promote_reuser_space.html")
  end

  def dataset_now_on_nap(email, dataset) do
    email
    |> common_email_options()
    |> subject("Votre jeu de donnÃ©es a Ã©tÃ© rÃ©fÃ©rencÃ© sur transport.data.gouv.fr")
    |> render_body("dataset_now_on_nap.html", %{
      dataset_url: TransportWeb.Router.Helpers.dataset_url(TransportWeb.Endpoint, :details, dataset.slug),
      dataset_custom_title: dataset.custom_title,
      contact_email_address: Application.get_env(:transport, :contact_email)
    })
  end

  def datasets_switching_climate_resilience_bill(
        email,
        datasets_previously_climate_resilience,
        datasets_now_climate_resilience
      ) do
    email
    |> common_email_options()
    |> subject("Loi climat et rÃ©silience : suivi des jeux de donnÃ©es")
    |> render_body("datasets_switching_climate_resilience_bill.html", %{
      datasets_now_climate_resilience: Enum.map(datasets_now_climate_resilience, &Enum.at(&1, 1)),
      datasets_previously_climate_resilience: Enum.map(datasets_previously_climate_resilience, &Enum.at(&1, 1))
    })
  end

  def multi_validation_with_error_notification(email, :producer, dataset: dataset, resources: resources) do
    email
    |> common_email_options()
    |> subject("Erreurs dÃ©tectÃ©es dans le jeu de donnÃ©es #{dataset.custom_title}")
    |> render_body("dataset_with_error_producer.html", dataset: dataset, resources: resources)
  end

  def multi_validation_with_error_notification(email, :reuser, dataset: dataset, producer_warned: producer_warned) do
    email
    |> common_email_options()
    |> subject("Erreurs dÃ©tectÃ©es dans le jeu de donnÃ©es #{dataset.custom_title}")
    |> render_body("dataset_with_error_reuser.html", dataset: dataset, producer_warned: producer_warned)
  end

  def resource_unavailable(email, :producer,
        dataset: dataset,
        hours_consecutive_downtime: hours_consecutive_downtime,
        deleted_recreated_on_datagouv: deleted_recreated_on_datagouv,
        resource_titles: resource_titles
      ) do
    email
    |> common_email_options()
    |> subject("Ressources indisponibles dans le jeu de donnÃ©es #{dataset.custom_title}")
    |> render_body("resource_unavailable_producer.html",
      dataset: dataset,
      hours_consecutive_downtime: hours_consecutive_downtime,
      deleted_recreated_on_datagouv: deleted_recreated_on_datagouv,
      resource_titles: resource_titles
    )
  end

  def resource_unavailable(email, :reuser,
        dataset: dataset,
        hours_consecutive_downtime: hours_consecutive_downtime,
        producer_warned: producer_warned,
        resource_titles: resource_titles
      ) do
    email
    |> common_email_options()
    |> subject("Ressources indisponibles dans le jeu de donnÃ©es #{dataset.custom_title}")
    |> render_body("resource_unavailable_reuser.html",
      dataset: dataset,
      hours_consecutive_downtime: hours_consecutive_downtime,
      producer_warned: producer_warned,
      resource_titles: resource_titles
    )
  end

  def periodic_reminder_producers_no_subscriptions(email, datasets) do
    email
    |> common_email_options()
    |> subject("Notifications pour vos donnÃ©es sur transport.data.gouv.fr")
    |> render_body("producer_without_subscriptions.html", %{datasets: datasets})
  end

  def periodic_reminder_producers_with_subscriptions(email, datasets_subscribed, other_producers_subscribers) do
    email
    |> common_email_options()
    |> subject("Rappel : vos notifications pour vos donnÃ©es sur transport.data.gouv.fr")
    |> render_body("producer_with_subscriptions.html", %{
      datasets_subscribed: datasets_subscribed,
      has_other_producers_subscribers: not Enum.empty?(other_producers_subscribers),
      other_producers_subscribers: Enum.map_join(other_producers_subscribers, ", ", &DB.Contact.display_name/1)
    })
  end

  def new_datasets(email, datasets) do
    dataset_link_fn = fn %DB.Dataset{} = dataset ->
      "* #{dataset.custom_title} - (#{DB.Dataset.type_to_str(dataset.type)}) - #{link(dataset)}"
    end

    text_content = """
    Bonjour,

    Les jeux de donnÃ©es suivants ont Ã©tÃ© rÃ©fÃ©rencÃ©s rÃ©cemment :

    #{datasets |> Enum.sort_by(& &1.type) |> Enum.map_join("\n", &dataset_link_fn.(&1))}

    Lâ€™Ã©quipe transport.data.gouv.fr
    """

    email
    |> common_email_options()
    |> subject("Nouveaux jeux de donnÃ©es rÃ©fÃ©rencÃ©s")
    |> text_body(text_content)
  end

  def expiration_producer(email, dataset, resources, delay) do
    email
    |> common_email_options()
    |> subject(email_subject(delay))
    |> render_body("expiration_producer.html",
      delay_str: delay_str(delay, :pÃ©riment),
      dataset: dataset,
      resource_titles: resource_titles(resources)
    )
  end

  defp common_email_options(email) do
    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(email)
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
  end

  defp link_and_name(%{"title" => title, "page" => page}) do
    ~s(* #{title} - #{page})
  end

  defp link(%DB.Dataset{slug: slug}), do: TransportWeb.Router.Helpers.dataset_url(TransportWeb.Endpoint, :details, slug)
end
