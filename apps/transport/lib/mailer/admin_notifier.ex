defmodule Transport.AdminNotifier do
  @moduledoc """
  Module in charge of building emails sent to the admin team (bizdev, tech, etc.)
  """
  use Phoenix.Swoosh, view: TransportWeb.EmailView

  def contact(email, subject, question) do
    notify_contact("PAN, Formulaire Contact", email)
    |> subject(subject)
    |> text_body(question)
  end

  def feedback(rating, explanation, email, feature) do
    rating_t = %{like: "jâ€™aime", neutral: "neutre", dislike: "mÃ©content"}

    reply_email = if email, do: email, else: Application.fetch_env!(:transport, :contact_email)

    feedback_content = """
    Vous avez un nouvel avis sur le PAN.
    FonctionnalitÃ© : #{feature}
    Notation : #{rating_t[rating]}
    Adresse e-mail : #{email}

    Explication : #{explanation}
    """

    notify_contact("Formulaire feedback", reply_email)
    |> subject("Nouvel avis pour #{feature} : #{rating_t[rating]}")
    |> text_body(feedback_content)
  end

  def bnlc_consolidation_report(subject, body, file_url) do
    report_content = """
    #{body}
    <br/><br/>
    ğŸ”— <a href="#{file_url}">Fichier consolidÃ©</a>
    """

    notify_bidzev()
    |> subject(subject)
    |> html_body(report_content)
  end

  def datasets_without_gtfs_rt_related_resources(datasets) do
    notify_bidzev()
    |> subject("Jeux de donnÃ©es GTFS-RT sans ressources liÃ©es")
    |> html_body("""
    <p>Bonjour,</p>

    <p>Les jeux de donnÃ©es suivants contiennent plusieurs GTFS et des liens entre les ressources GTFS-RT et GTFS sont manquants :</p>

    <ul>
    #{Enum.map_join(datasets, "", &link_and_name/1)}
    </ul>

    <p>Lâ€™Ã©quipe transport.data.gouv.fr</p>
    """)
  end

  def unknown_gbfs_operator_feeds(resources) do
    notify_bidzev()
    |> subject("Flux GBFS : opÃ©rateurs non dÃ©tectÃ©s")
    |> html_body("""
    <p>Bonjour,</p>

    <p>Il n'est pas possible de dÃ©tecter automatiquement les opÃ©rateurs des flux GBFS suivants :</p>

    <ul>
    #{Enum.map(resources, fn %DB.Resource{url: url} -> ~s|<li><a href="#{url}">#{url}</a></li>| end)}
    </ul>

    <p>La configuration peut Ãªtre modifiÃ©e <a href="https://github.com/etalab/transport-site/blob/master/apps/transport/priv/gbfs_operators.csv">sur GitHub</a>.</p>

    <p>Lâ€™Ã©quipe transport.data.gouv.fr</p>
    """)
  end

  def datasets_climate_resilience_bill_inappropriate_licence(datasets) do
    notify_bidzev()
    |> subject("Jeux de donnÃ©es article 122 avec licence inappropriÃ©e")
    |> render_body("datasets_climate_resilience_bill_inappropriate_licence.html", %{datasets: datasets})
  end

  def new_datagouv_datasets(category, datagouv_datasets, rule_explanation, duration) do
    notify_bidzev()
    |> subject("Nouveaux jeux de donnÃ©es #{category} Ã  rÃ©fÃ©rencer - data.gouv.fr")
    |> html_body("""
    <p>Bonjour,</p>

    <p>Les jeux de donnÃ©es suivants ont Ã©tÃ© ajoutÃ©s sur data.gouv.fr dans les derniÃ¨res #{duration}h et sont susceptibles d'avoir leur place sur le PAN :</p>

    <ul>
    #{Enum.map_join(datagouv_datasets, "", &link_and_name_from_datagouv_payload/1)}
    </ul>
    <br/>
    <hr>
    #{rule_explanation}
    <p>Vous pouvez modifier <a href="https://github.com/etalab/transport-site/blob/master/apps/transport/lib/jobs/new_datagouv_datasets_job.ex">les rÃ¨gles de cette tÃ¢che</a>.</p>
    """)
  end

  def expiration(records) do
    notify_bidzev()
    |> subject("Jeux de donnÃ©es arrivant Ã  expiration")
    |> html_body("""
    <p>Bonjour,</p>

    <p>Voici un rÃ©sumÃ© des jeux de donnÃ©es arrivant Ã  expiration</p>

    #{Enum.map_join(records, "<hr>", &expiration_str/1)}
    """)
  end

  def inactive_datasets(reactivated_datasets, inactive_datasets, archived_datasets) do
    reactivated_datasets_str = fmt_reactivated_datasets(reactivated_datasets)
    inactive_datasets_str = fmt_inactive_datasets(inactive_datasets)
    archived_datasets_str = fmt_archived_datasets(archived_datasets)

    notify_bidzev()
    |> subject("Jeux de donnÃ©es supprimÃ©s ou archivÃ©s")
    |> html_body("""
    <p>Bonjour,</p>
    #{inactive_datasets_str}
    #{reactivated_datasets_str}
    #{archived_datasets_str}

    <p>Il faut peut Ãªtre creuser pour savoir si c'est normal.</p>
    """)
  end

  def oban_failure(worker) do
    notify_tech()
    |> subject("Ã‰chec de job Oban : #{worker}")
    |> text_body("Un job Oban #{worker} vient d'Ã©chouer, il serait bien d'investiguer.")
  end

  # Utility functions from here

  defp notify_bidzev do
    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    # Uses the contact@ email address but method is kept if we need
    # to route differently in the future.
    |> to(Application.fetch_env!(:transport, :contact_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
  end

  defp notify_tech do
    new()
    |> from({"transport.data.gouv.fr", Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :tech_email))
    |> reply_to(Application.fetch_env!(:transport, :contact_email))
  end

  defp notify_contact(form_name, email) do
    new()
    |> from({form_name, Application.fetch_env!(:transport, :contact_email)})
    |> to(Application.fetch_env!(:transport, :contact_email))
    |> reply_to(email)
  end

  defp expiration_str({delay, records}) do
    datasets = Enum.map(records, fn {%DB.Dataset{} = d, _} -> d end)

    dataset_str = fn %DB.Dataset{} = dataset ->
      link_and_name(dataset, " - #{expiration_notification_enabled_str(dataset)}#{climate_resilience_str(dataset)}")
    end

    """
    <p>Jeux de donnÃ©es #{delay_str(delay, :pÃ©rimant)} :</p>

    <ul>
    #{Enum.map_join(datasets, "\n", &dataset_str.(&1))}
    </ul>
    """
  end

  def expiration_notification_enabled_str(%DB.Dataset{} = dataset) do
    if has_expiration_notifications?(dataset) do
      "âœ… notification automatique"
    else
      "âŒ pas de notification automatique"
    end
  end

  defp climate_resilience_str(%DB.Dataset{} = dataset) do
    if DB.Dataset.climate_resilience_bill?(dataset) do
      " âš–ï¸ğŸ—ºï¸ article 122"
    else
      ""
    end
  end

  def has_expiration_notifications?(%DB.Dataset{} = dataset) do
    Transport.NotificationReason.reason(:expiration)
    |> DB.NotificationSubscription.subscriptions_for_reason_dataset_and_role(dataset, :producer)
    |> Enum.count() > 0
  end

  defp fmt_inactive_datasets([]), do: ""

  defp fmt_inactive_datasets(inactive_datasets) do
    """
    <p>Certains jeux de donnÃ©es ont disparus de data.gouv.fr :</p>
    <ul>
    #{Enum.map_join(inactive_datasets, "", &link_and_name/1)}
    </ul>
    """
  end

  defp fmt_reactivated_datasets([]), do: ""

  defp fmt_reactivated_datasets(reactivated_datasets) do
    """
    <p>Certains jeux de donnÃ©es disparus sont rÃ©apparus sur data.gouv.fr :</p>
    <ul>
    #{Enum.map_join(reactivated_datasets, "", &link_and_name/1)}
    </ul>
    """
  end

  defp fmt_archived_datasets([]), do: ""

  defp fmt_archived_datasets(archived_datasets) do
    """
    <p>Certains jeux de donnÃ©es sont indiquÃ©s comme archivÃ©s sur data.gouv.fr :</p>
    <ul>
    #{Enum.map_join(archived_datasets, "", &link_and_name/1)}
    </ul>

    <p>#{count_archived_datasets()} jeux de donnÃ©es sont archivÃ©s. Retrouvez-les <a href="#{backoffice_archived_datasets_url()}">dans le backoffice</a>.</p>
    """
  end

  def count_archived_datasets do
    DB.Dataset.archived() |> DB.Repo.aggregate(:count, :id)
  end

  defp backoffice_archived_datasets_url do
    TransportWeb.Router.Helpers.backoffice_page_url(TransportWeb.Endpoint, :index, %{"filter" => "archived"}) <>
      "#list_datasets"
  end

  @doc """
  Common to both notifiers. If refactored or moved elsewhere, donâ€™t forget to change or delete Transport.NotifiersTest.
  iex> delay_str(0, :pÃ©rimant)
  "pÃ©rimant demain"
  iex> delay_str(0, :pÃ©riment)
  "pÃ©riment demain"
  iex> delay_str(2, :pÃ©rimant)
  "pÃ©rimant dans 2 jours"
  iex> delay_str(2, :pÃ©riment)
  "pÃ©riment dans 2 jours"
  iex> delay_str(-1, :pÃ©rimant)
  "pÃ©rimÃ© depuis hier"
  iex> delay_str(-1, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis hier"
  iex> delay_str(-2, :pÃ©rimant)
  "pÃ©rimÃ©s depuis 2 jours"
  iex> delay_str(-2, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis 2 jours"
  iex> delay_str(-60, :pÃ©riment)
  "sont pÃ©rimÃ©es depuis 60 jours"
  """
  @spec delay_str(integer(), :pÃ©riment | :pÃ©rimant) :: binary()
  def delay_str(0, verb), do: "#{verb} demain"
  def delay_str(1, verb), do: "#{verb} dans 1 jour"
  def delay_str(d, verb) when d >= 2, do: "#{verb} dans #{d} jours"
  def delay_str(-1, :pÃ©rimant), do: "pÃ©rimÃ© depuis hier"
  def delay_str(-1, :pÃ©riment), do: "sont pÃ©rimÃ©es depuis hier"
  def delay_str(d, :pÃ©rimant) when d <= -2, do: "pÃ©rimÃ©s depuis #{-d} jours"
  def delay_str(d, :pÃ©riment) when d <= -2, do: "sont pÃ©rimÃ©es depuis #{-d} jours"

  defp link_and_name_from_datagouv_payload(%{"title" => title, "page" => page}) do
    link = Phoenix.HTML.Link.link(title, to: page) |> Phoenix.HTML.safe_to_string()
    "<li>#{link}</li>"
  end

  @spec link_and_name(DB.Dataset.t(), binary()) :: binary()
  defp link_and_name(%DB.Dataset{slug: slug, custom_title: custom_title}, extra_content \\ "") do
    url = TransportWeb.Router.Helpers.dataset_url(TransportWeb.Endpoint, :details, slug)
    link = Phoenix.HTML.Link.link(custom_title, to: url) |> Phoenix.HTML.safe_to_string()
    "<li>#{link}#{extra_content}</li>"
  end
end
